/*
 * Copyright (C) 2016 Intelligent Information Systems Group.
 *
 * This file is part of AgE.
 *
 * AgE is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * AgE is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with AgE.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'ch.raffael.pegdown-doclet:pegdown-doclet:1.3'
	}
}

plugins {
	id 'net.ltgt.errorprone' version '0.0.9'
	id 'net.researchgate.release' version '2.5.0'
}

project.ext.hazelcastVersion = '3.8'
project.ext.springVersion = '4.3'
project.ext.jmhVersion = '1.18'

allprojects {
	group 'pl.edu.agh.age'
	version '0.4-SNAPSHOT'
	apply plugin: 'maven-publish'
	apply plugin: 'ch.raffael.pegdown-doclet'

	repositories {
		mavenCentral()
	}

	release {
		git {
			requireBranch = 'release/[0-9.]+'
		}
	}

	publishing {
		repositories {
			maven {
				if (project.version.endsWith('-SNAPSHOT')) {
					url "${project.properties.nexusUrl}/repository/maven-snapshots"
				} else {
					url "${project.properties.nexusUrl}/repository/maven-releases"
				}
				credentials {
					username = project.properties.nexusUsername
					password = project.properties.nexusPassword
				}
			}
		}
	}

	configurations.all {
		// As in: https://docs.gradle.org/current/userguide/dependency_management.html#sec:custom_versioning_scheme
		resolutionStrategy.eachDependency { DependencyResolveDetails details ->
			if (details.requested.group == 'com.hazelcast' && details.requested.version == 'default') {
				details.useVersion "${hazelcastVersion}+"
			}
			if (details.requested.group == 'org.springframework' && details.requested.version == 'default') {
				details.useVersion "${springVersion}+"
			}
			// Enforce JMH version (overrides plugin defaults)
			if (details.requested.group == 'org.openjdk.jmh') {
				details.useVersion "${jmhVersion}"
			}
		}
	}
}

// Mock so gradle-release stop complaining
task build() {

}

subprojects {
	apply plugin: 'java'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	configurations {
		checkerFrameworkAnnotatedJDK
		checkerFramework
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
			}
		}
	}

	dependencies {
		compile 'javax.inject:javax.inject:1'
		compile 'org.slf4j:slf4j-api:+'
		compile 'org.slf4j:jcl-over-slf4j:+'
		compile 'com.google.code.findbugs:jsr305:+'
		compile 'io.javaslang:javaslang:2.+'
		compile 'org.apache.commons:commons-lang3:+'
		compile 'com.google.guava:guava:+'
		compile 'one.util:streamex:+'

		runtime 'org.codehaus.groovy:groovy:+'
		compile 'ch.qos.logback:logback-classic:+'
		compile 'org.logback-extensions:logback-ext-spring:+'

		testCompile 'junit:junit:+'
		testCompile 'org.mockito:mockito-core:+'
		testCompile 'org.assertj:assertj-core:+'

		checkerFrameworkAnnotatedJDK 'org.checkerframework:jdk8:+'
		checkerFramework 'org.checkerframework:checker:+'
		compile 'org.checkerframework:checker-qual:+'
	}

	if (project.hasProperty('withChecker')) {
		println('Compiling with checker framework')
		tasks.withType(JavaCompile).all { JavaCompile compile ->
			compile.options.compilerArgs = [
				'-processor', 'org.checkerframework.checker.nullness.NullnessChecker',
				'-processorpath', "${configurations.checkerFramework.asPath}",
				// uncomment to turn Checker Framework errors into warnings
				// '-Awarns',
				'-AassumeAssertionsAreEnabled',
				'-Astubs=checkerframework/stubs',
				"-Xbootclasspath/p:${configurations.checkerFrameworkAnnotatedJDK.asPath}"
			]
		}
	}
}

def exportedProjects = [
	":age3-console",
	":age3-core",
	":age3-examples",
	":age3-stream-agents",
]

task alljavadoc(type: Javadoc, group: "Documentation") {
	description = 'Generates a global javadoc from all the modules'
	source exportedProjects.collect { project(it).sourceSets.main.allJava }
	classpath = files(exportedProjects.collect { project(it).sourceSets.main.compileClasspath })
	options.memberLevel = JavadocMemberLevel.PRIVATE
	options.links 'http://docs.oracle.com/javase/8/docs/api/'
	options.links 'http://docs.spring.io/spring/docs/current/javadoc-api/'
	options.links 'https://google.github.io/guava/releases/snapshot/api/docs/'
	options.links "http://docs.hazelcast.org/docs/${hazelcastVersion}/javadoc/"
	options.tags = ['apiNote:a:API Note:', 'implSpec:a:Implementation Requirements:', 'implNote:a:Implementation Note:']
	destinationDir = file("${buildDir}/docs/javadoc")
}


