/*
 * Copyright (C) 2016 Intelligent Information Systems Group.
 *
 * This file is part of AgE.
 *
 * AgE is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * AgE is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with AgE.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
	id 'application'
	id 'com.github.johnrengelman.shadow' version '1.2.3'
	id 'me.champeau.gradle.jmh' version '0.3.1'
}

mainClassName = 'pl.edu.agh.age.node.NodeBootstrapper'
applicationDefaultJvmArgs = [
	'-enableassertions',
	'-Dhazelcast.logging.type=slf4j',
	'-Dlogback.configurationFile=pl/edu/agh/age/node/logback.groovy'
]

shadowJar {
	// Required for merging spring handler definitions (e.g. from Hazelcast)
	mergeServiceFiles('META-INF/spring.*')
}

jmh {
	verbosity = 'EXTRA'
}

dependencies {
	compile project(':age3-core')

	compile 'org.openjdk.jmh:jmh-core:+'
	compile 'org.openjdk.jmh:jmh-generator-annprocess:+'
	testCompile 'org.springframework:spring-test:+'
	testCompile "org.mockito:mockito-core:1.+"
}

jmh {
	includeTests = false
	duplicateClassesStrategy = 'warn'
}

jmhJar {
	mergeServiceFiles('META-INF/spring.*')
	exclude 'LICENSE'
}

task node(type: JavaExec) {
	main = 'pl.edu.agh.age.node.NodeBootstrapper'
	classpath = sourceSets.main.runtimeClasspath
	standardOutput = System.out
	standardInput = System.in
	jvmArgs '-enableassertions'
	systemProperty 'logback.configurationFile', 'pl/edu/agh/age/compute/stream/logback.groovy'
	if (project.hasProperty('appArgs')) {
		args Eval.me(appArgs)
	}
}
