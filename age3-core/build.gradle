/*
 * Copyright (C) 2016 Intelligent Information Systems Group.
 *
 * This file is part of AgE.
 *
 * AgE is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * AgE is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with AgE.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
	id 'application'
	id 'com.github.johnrengelman.shadow' version '1.2.4'
}

mainClassName = 'pl.edu.agh.age.node.NodeBootstrapper'
applicationDefaultJvmArgs = [
	'-enableassertions',
]

shadowJar {
	// Required for merging spring handler definitions (e.g. from Hazelcast)
	mergeServiceFiles('META-INF/spring.*')
	baseName = 'age3-core'
	classifier = null
	version = null
}

distShadowTar {
	version = null
}

distShadowZip {
	version = null
}

dependencies {
	compile 'org.springframework:spring-context:+'
	compile('com.hazelcast:hazelcast:default') {
		// Not needed and would introduce a lot of duplicated annotations
		exclude group: 'net.sourceforge.findbugs'
	}
	compile 'org.jgrapht:jgrapht-core:+'

	runtime 'javax.cache:cache-api:+'

	testCompile 'org.springframework:spring-test:+'
}

task node(type: JavaExec) {
	main = 'pl.edu.agh.age.node.NodeBootstrapper'
	classpath = sourceSets.main.runtimeClasspath
	standardOutput = System.out
	standardInput = System.in
	jvmArgs '-enableassertions'
	if (project.hasProperty('appArgs')) {
		args Eval.me(appArgs)
	}
}
